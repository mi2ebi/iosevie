$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Letter-Cyrillic-E : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Latin-C : CLetterForm CConfig
	glyph-block-import Letter-Cyrillic-Iotified-A : Iotified
	glyph-block-import Letter-Latin-Upper-F : EFVJutLength

	foreach { suffix { sty styBot fMidSerif } } [Object.entries CConfig] : do
		define [CyrlEShepe fCapital df top ada adb] : glyph-proc
			local sw : Math.min df.mvs : AdviceStroke2 2 3 top df.adws
			local lf : CLetterForm df sty styBot top 0
				ada -- ada
				adb -- adb
				sw  -- sw
			include : lf.revFull

			local xMidBarLeft : if (fMidSerif && !fCapital) df.middle : mix df.leftSB df.rightSB 0.35
			include : HBar.m xMidBarLeft ((df.rightSB - OX) + O) (top / 2) sw

			if fMidSerif : begin
				local { jutTop jutBot jutMid } : EFVJutLength top 0.5 sw [if fCapital Hook nothing]
				local swVJut : Math.min (VJutStroke * (sw / Stroke))
					VSwToH : [if fCapital 0.800 0.625] * ((df.rightSB - [HSwToV sw]) - [if fCapital xMidBarLeft : df.leftSB + [HSwToV sw]])
				include : VSerif.ml (xMidBarLeft - [if fCapital 0 : HSwToV : 0.5 * swVJut]) (top / 2) jutMid swVJut

		create-glyph "cyrl/E.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : CyrlEShepe true [DivFrame 1] CAP ArchDepthA ArchDepthB

		create-glyph "cyrl/e.\(suffix)" : glyph-proc
			include : MarkSet.e
			set-base-anchor 'cvDecompose' 0 0
			include : CyrlEShepe false [DivFrame 1] XH SmallArchDepthA SmallArchDepthB

		define [CyrlYeShape fCapital df top ada adb] : glyph-proc
			local sw : Math.min df.mvs : AdviceStroke2 2 3 top df.adws
			local lf : CLetterForm df sty styBot top 0
				ada -- ada
				adb -- adb
				sw  -- sw
			include : lf.full

			local xMidBarRight : if (fMidSerif && !fCapital) df.middle : mix df.rightSB df.leftSB 0.35
			include : HBar.m ((df.leftSB + OX) - O) xMidBarRight (top / 2) sw

			if fMidSerif : begin
				local { jutTop jutBot jutMid } : EFVJutLength top 0.5 sw [if fCapital Hook nothing]
				local swVJut : Math.min (VJutStroke * (sw / Stroke))
					VSwToH : [if fCapital 0.800 0.625] * ([if fCapital xMidBarRight : df.rightSB - [HSwToV sw]] - (df.leftSB + [HSwToV sw]))
				include : VSerif.mr (xMidBarRight + [if fCapital 0 : HSwToV : 0.5 * swVJut]) (top / 2) jutMid swVJut

		create-glyph "cyrl/Ye.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : CyrlYeShape true [DivFrame 1] CAP ArchDepthA ArchDepthB

		create-glyph "cyrl/ye.\(suffix)" : glyph-proc
			include : MarkSet.e
			set-base-anchor 'cvDecompose' 0 0
			include : CyrlYeShape false [DivFrame 1] XH SmallArchDepthA SmallArchDepthB

		define [IotifiedEShape fCapital df top ada adb] : glyph-proc
			local sw : Math.min df.mvs : AdviceStroke2 2 3 top (0.75 * df.adws)
			local gap : 0.375 * (df.rightSB - df.leftSB) - [HSwToV : 0.25 * sw] + sw / 16
			local subDf : DivFrame [Math.min ((df.width - gap) / Width) (0.75 * df.adws)] 2

			local lf : CLetterForm subDf sty styBot top 0
				ada -- (ada * 0.75 * df.adws)
				adb -- (adb * 0.75 * df.adws)
				sw  -- sw

			local shift : df.width - subDf.width
			include : with-transform [ApparentTranslate shift 0] : lf.full

			local xMidBarRight : [if (fMidSerif && !fCapital) subDf.middle : mix subDf.rightSB subDf.leftSB 0.35] + shift
			include : Iotified.full df top
				hBarRight -- xMidBarRight
				hBarY     -- (top / 2)
				fCapital  -- fCapital
				swRef     -- sw

			if fMidSerif : begin
				local { jutTop jutBot jutMid } : EFVJutLength top 0.5 sw [if fCapital Hook nothing]
				local swVJut : Math.min (VJutStroke * (sw / Stroke))
					VSwToH : [if fCapital 0.800 0.625] * ([if fCapital xMidBarRight : (subDf.rightSB - [HSwToV sw]) + shift] - ((subDf.leftSB + [HSwToV sw]) + shift))
				include : VSerif.mr (xMidBarRight + [if fCapital 0 : HSwToV : 0.5 * swVJut]) (top / 2) jutMid swVJut

		create-glyph "cyrl/EIotified.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.capital
			include : IotifiedEShape true df CAP ArchDepthA ArchDepthB

		create-glyph "cyrl/eIotified.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.e
			include : IotifiedEShape false df XH SmallArchDepthA SmallArchDepthB

	select-variant 'cyrl/E'  0x42D
	select-variant 'cyrl/e'  0x44D
	select-variant 'cyrl/Ye' 0x404
	select-variant 'cyrl/ye' 0x454
	select-variant "cyrl/EIotified" 0x464 (follow -- 'cyrl/Ye')
	select-variant "cyrl/eIotified" 0x465 (follow -- 'cyrl/ye')
