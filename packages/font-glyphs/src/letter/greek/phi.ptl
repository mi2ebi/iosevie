$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"
import [DesignParameters] from "../../meta/aesthetics.mjs"

glyph-module

glyph-block Letter-Greek-Phi : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : ExtendAboveBaseAnchors ExtendBelowBaseAnchors
	glyph-block-import Letter-Shared-Metrics : BowlXDepth
	glyph-block-import Letter-Shared-Shapes : FlatHookDepth DiagTail OBarLeft OBarRight

	define [VarPhiRing fFlatTB df y2 y3 ada adb] : glyph-proc
		include : VBar.m df.middle y2 y3 df.mvs
		if fFlatTB
		: then : begin
			local mul : 1 - 0.75 * (df.adws % 0.5)
			local l : mix 0        df.leftSB  mul
			local r : mix df.width df.rightSB mul
			local turnRadius : BowlXDepth y3 y2 l (df.middle + [HSwToV HalfStroke]) df.mvs
			include : OShapeFlatTB y3 y2 l r df.mvs ada adb : Math.max
				(r - l) - 2 * turnRadius
				HSwToV : clamp df.mvs Stroke : VSwToH : (r - l) - [HSwToV : 2.5 * df.mvs]
		: else : begin
			include : OShape y3 y2 df.leftSB df.rightSB df.mvs ada adb
		define swBar Stroke
		return : object swBar

	define [CyrlEfSplitRing fFlatTB df y2 y3 ada adb] : glyph-proc
		include : VBar.m df.middle y2 y3 df.mvs
		local subDf : df.slice 3 2 OX
		include : OBarRight.shape
			top       -- y3
			bot       -- y2
			left      -- df.leftSB
			right     -- (df.middle + [HSwToV : 0.5 * df.mvs])
			sw        -- df.mvs
			fine      -- df.shoulderFine
			ada       -- subDf.smallArchDepthA
			adb       -- subDf.smallArchDepthB
		include : OBarLeft.shape
			top       -- y3
			bot       -- y2
			left      -- (df.middle - [HSwToV : 0.5 * df.mvs])
			right     -- df.rightSB
			sw        -- df.mvs
			fine      -- df.shoulderFine
			ada       -- subDf.smallArchDepthA
			adb       -- subDf.smallArchDepthB
		define swBar df.mvs
		return : object swBar

	define [GrekLowerPhiCursiveRing fFlatTB df y2 y3 ada adb] : glyph-proc
		local l : df.leftSB  + OX * 2
		local r : df.rightSB - OX * 2
		include : dispiro
			widths.lhs df.mvs
			g4 [mix df.leftSB df.rightSB 0.1] y3
			g4 l [mix y2 y3 0.55]
			arch.lhs y2 (sw -- df.mvs)
			g4 r [mix y2 y3 0.55]
			arcvh 8
			g4.left.mid [mix r (df.middle - [HSwToV : 0.5 * df.mvs]) 0.525] y3 [heading Leftward]
			archv
			flat (df.middle - [HSwToV : 0.5 * df.mvs]) [mix y2 y3 0.66]
			curl (df.middle - [HSwToV : 0.5 * df.mvs]) (y2 + 0.2 * df.mvs) [heading Downward]
		define swBar Stroke
		return : object swBar

	define [StraightBar df y1 y2 y3 y4 sw] : glyph-proc
		include : VBar.m df.middle y1 (y2 + HalfStroke) sw
		include : VBar.m df.middle (y3 - HalfStroke) y4 sw

	define [CursiveBar df y1 y2 y3 y4 sw] : glyph-proc
		local dfHook : DivFrame [mix 1 (1 / df.adws) 0.5]
		local hd : FlatHookDepth dfHook

		local mul : mix 1 (1 / df.adws) 0.75
		local xCrossLeft  : [mix 0            dfHook.leftSB  mul] + (df.middle - dfHook.middle)
		local xCrossRight : [mix dfHook.width dfHook.rightSB mul] + (df.middle - dfHook.middle)

		local xBarLeft    : df.middle - [HSwToV : 0.5 * sw]
		local xBarRight   : df.middle + [HSwToV : 0.5 * sw]

		include : dispiro
			widths.lhs sw
			flat xCrossRight y4
			curl [Math.min (xBarLeft + hd.x) (xCrossRight - TINY)] y4
			archv
			flat xBarLeft [Math.max y3 (y4 - hd.y)]
			curl xBarLeft (y3 + O)

		include : dispiro
			widths.lhs sw
			flat xCrossLeft y1
			curl [Math.max (xBarRight - hd.x) (xCrossLeft + TINY)] y1
			archv
			flat xBarRight [Math.min y2 (y1 + hd.y)]
			curl xBarRight (y2 - O)

	define [DiagonalTailCursiveBar df y1 y2 y3 y4 sw] : glyph-proc
		local dfHook : DivFrame [mix 1 (1 / df.adws) 0.5]
		local hd : FlatHookDepth dfHook

		local mul : mix 1 (1 / df.adws) 0.75
		local xCrossRight : [mix dfHook.width dfHook.rightSB mul] + (df.middle - dfHook.middle)
		local xBarLeft    : df.middle - [HSwToV : 0.5 * sw]

		include : dispiro
			widths.lhs sw
			flat xCrossRight y4
			curl [Math.min (xBarLeft + hd.x) (xCrossRight - TINY)] y4
			archv
			flat xBarLeft [Math.max y3 (y4 - hd.y)]
			curl xBarLeft (y3 + O)

		include : dispiro
			widths.center sw
			flat df.middle (y2 - O) [heading Downward]
			DiagTail.L df.middle y1 [DiagTail.StdDepth dfHook sw] sw

	define [MtSerif df y4 sw] : tagged 'serifMT' : HSerif.lt df.middle y4 (Jut          - [HSwToV : 0.5 * (Stroke - sw)]) sw
	define [MbSerif df y1 sw] : tagged 'serifMB' : HSerif.mb df.middle y1 (MidJutCenter - [HSwToV : 0.5 * (Stroke - sw)]) sw

	glyph-block-export yCapitalPhiBowlBot yCapitalPhiBowlTop
	define [yCapitalPhiBowlBot y1 y4 slab] : mix (y1 + [if slab Stroke 0]) (y4 - [if slab Stroke 0]) 0.125
	define [yCapitalPhiBowlTop y1 y4 slab] : mix (y1 + [if slab Stroke 0]) (y4 - [if slab Stroke 0]) 0.875

	define [GrekCapitalPhiImpl fFlatTB df] : glyph-proc
		local y2 : yCapitalPhiBowlBot 0 CAP SLAB
		local y3 : yCapitalPhiBowlTop 0 CAP SLAB
		local dim : include : VarPhiRing fFlatTB df y2 y3 df.archDepthA df.archDepthB
		include : StraightBar df 0 y2 y3 CAP dim.swBar

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle CAP MidJutCenter dim.swBar
			include : tagged 'serifMB' : HSerif.mb df.middle 0   MidJutCenter dim.swBar

	create-glyph 'grek/Phi' 0x3A6 : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.capital
		include : GrekCapitalPhiImpl false df

	create-glyph 'cyrl/Ef' 0x424 : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.capital
		include : GrekCapitalPhiImpl true df

	create-glyph 'cyrl/Ef.BGR' : glyph-proc
		local df : include : DivFrame para.advanceScaleMM 3
		include : df.markSet.capital

		local y1 : 0   - (LongVJut - QuarterStroke)
		local y4 : CAP + (LongVJut - QuarterStroke)

		include : ExtendBelowBaseAnchors y1
		include : ExtendAboveBaseAnchors y4

		local pseudoY2 : yCapitalPhiBowlBot 0 CAP SLAB
		local pseudoY3 : yCapitalPhiBowlTop 0 CAP SLAB

		local y2 : Math.max 0   : y1 + (pseudoY2 - 0)
		local y3 : Math.min CAP : y4 - (CAP - pseudoY3)

		local pArchDepth : mix 1 ((y3 - y2) / (pseudoY3 - pseudoY2)) (2 * (df.adws - 1))

		local ada : df.archDepthAOf : ArchDepth * pArchDepth
		local adb : df.archDepthBOf : ArchDepth * pArchDepth

		local dim : include : VarPhiRing true df y2 y3 ada adb
		include : StraightBar df y1 y2 y3 y4 dim.swBar

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle y4 MidJutCenter dim.swBar
			include : tagged 'serifMB' : HSerif.mb df.middle y1 MidJutCenter dim.swBar

	create-glyph 'taillessphi' 0x2C77 : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.e
		include : GrekLowerPhiCursiveRing false df 0 XH df.smallArchDepthA df.smallArchDepthB

	create-glyph 'grek/phi.cursive' : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.p
		local dim : include : GrekLowerPhiCursiveRing false df 0 XH df.smallArchDepthA df.smallArchDepthB
		include : VBar.m df.middle Descender (0.2 * df.mvs) dim.swBar

	create-glyph 'grek/phi.straight' : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.bp
		local dim : include : VarPhiRing false df 0 XH df.smallArchDepthA df.smallArchDepthB
		include : StraightBar df Descender 0 XH Ascender dim.swBar

	create-glyph 'grek/phi.neohellenic' : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.p
		local dim : include : VarPhiRing false df 0 XH df.smallArchDepthA df.smallArchDepthB
		include : VBar.m df.middle Descender (0.2 * df.mvs) dim.swBar

	select-variant 'grek/phi' 0x3C6
	alias 'grek/varphi' 0x3D5 'grek/phi.straight'

	create-glyph 'latn/phi' 0x278 : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include [refer-glyph 'grek/varphi'] AS_BASE ALSO_METRICS

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle Ascender  MidJutCenter
			include : tagged 'serifMB' : HSerif.mb df.middle Descender MidJutCenter

	define CyrlLowerEfConfig : SuffixCfg.weave
		object # bowl
			""                    { VarPhiRing      para.advanceScaleM  }
			splitBowl             { CyrlEfSplitRing para.advanceScaleMM }
		object # bar
			serifless             { StraightBar null    null    }
			topSerifed            { StraightBar MtSerif null    }
			serifed               { StraightBar MtSerif MbSerif }
			cursive               { CursiveBar  null    null    }
			diagonalTailedCursive { DiagonalTailCursiveBar null null }

	foreach { suffix { { Bowl adws } { Bar sMT sMB } } } [Object.entries CyrlLowerEfConfig] : do
		create-glyph "cyrl/ef.\(suffix)" : glyph-proc
			local df : include : DivFrame adws 3
			include : df.markSet.bp
			local dim : include : Bowl true df 0 XH df.smallArchDepthA df.smallArchDepthB
			include : Bar df Descender 0 XH Ascender dim.swBar
			if sMT : include : sMT df Ascender  dim.swBar
			if sMB : include : sMB df Descender dim.swBar

	select-variant 'cyrl/ef' 0x444
	select-variant 'cyrl/ef.BGR' (shapeFrom -- 'cyrl/ef')
