$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"
import [DesignParameters] from "../../meta/aesthetics.mjs"

glyph-module

glyph-block Letter-Greek-Phi : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : ExtendAboveBaseAnchors ExtendBelowBaseAnchors
	glyph-block-import Letter-Shared-Metrics : BowlXDepth
	glyph-block-import Letter-Shared-Shapes : FlatHookDepth DiagTail OBarLeft OBarRight

	define [VarPhiRing fFlatTB df y2 y3 ada adb] : glyph-proc
		include : VBar.m df.middle y2 y3 df.mvs
		if fFlatTB
		: then : begin
			local mul : 1 - 0.75 * (df.adws % 0.5)
			local l : mix 0        df.leftSB  mul
			local r : mix df.width df.rightSB mul
			local turnRadius : BowlXDepth y3 y2 l (df.middle + [HSwToV HalfStroke]) df.mvs
			include : OShapeFlatTB y3 y2 l r df.mvs ada adb [Math.max ((r - l) - 2 * turnRadius) : HSwToV Stroke]
		: else : begin
			include : OShape y3 y2 df.leftSB df.rightSB df.mvs ada adb
		define swBar Stroke
		return : object swBar

	define [CyrlEfSplitRing fFlatTB df y2 y3 ada adb] : glyph-proc
		include : VBar.m df.middle y2 y3 df.mvs
		local subDf : df.slice 3 2 OX
		include : OBarRight.shape
			top       -- y3
			bot       -- y2
			left      -- df.leftSB
			right     -- (df.middle + [HSwToV : 0.5 * df.mvs])
			sw        -- df.mvs
			fine      -- df.shoulderFine
			ada       -- subDf.smallArchDepthA
			adb       -- subDf.smallArchDepthB
		include : OBarLeft.shape
			top       -- y3
			bot       -- y2
			left      -- (df.middle - [HSwToV : 0.5 * df.mvs])
			right     -- df.rightSB
			sw        -- df.mvs
			fine      -- df.shoulderFine
			ada       -- subDf.smallArchDepthA
			adb       -- subDf.smallArchDepthB
		define swBar df.mvs
		return : object swBar

	define [GrekLowerPhiCursiveRing fFlatTB df y2 y3 ada adb] : glyph-proc
		local l : df.leftSB  + OX * 2
		local r : df.rightSB - OX * 2
		include : dispiro
			widths.lhs df.mvs
			g4 [mix df.leftSB df.rightSB 0.1] y3
			g4 l [mix y2 y3 0.55]
			arch.lhs y2 (sw -- df.mvs)
			g4 r [mix y2 y3 0.55]
			arcvh 8
			g4.left.mid [mix r (df.middle - [HSwToV : 0.5 * df.mvs]) 0.525] y3 [heading Leftward]
			archv
			flat (df.middle - [HSwToV : 0.5 * df.mvs]) [mix y2 y3 0.66]
			curl (df.middle - [HSwToV : 0.5 * df.mvs]) (y2 + 0.2 * df.mvs) [heading Downward]
		define swBar Stroke
		return : object swBar

	define [StraightBar df y1 y2 y3 y4 sw] : glyph-proc
		include : VBar.m df.middle y1 (y2 + HalfStroke) sw
		include : VBar.m df.middle (y3 - HalfStroke) y4 sw

	define [CursiveBar df y1 y2 y3 y4 sw] : glyph-proc
		local dfHook : DivFrame [mix 1 (1 / df.adws) 0.5]
		local hd : FlatHookDepth dfHook

		local mul : mix 1 (1 / df.adws) 0.75
		local xCrossLeft  : [mix 0            dfHook.leftSB  mul] + (df.middle - dfHook.middle)
		local xCrossRight : [mix dfHook.width dfHook.rightSB mul] + (df.middle - dfHook.middle)

		local xBarLeft    : df.middle - [HSwToV : 0.5 * sw]
		local xBarRight   : df.middle + [HSwToV : 0.5 * sw]

		include : dispiro
			widths.lhs sw
			flat xCrossRight y4
			curl [Math.min (xBarLeft + hd.x) (xCrossRight - TINY)] y4
			archv
			flat xBarLeft [Math.max y3 (y4 - hd.y)]
			curl xBarLeft (y3 + O)

		include : dispiro
			widths.lhs sw
			flat xCrossLeft y1
			curl [Math.max (xBarRight - hd.x) (xCrossLeft + TINY)] y1
			archv
			flat xBarRight [Math.min y2 (y1 + hd.y)]
			curl xBarRight (y2 - O)

	define [DiagonalTailCursiveBar df y1 y2 y3 y4 sw] : glyph-proc
		local dfHook : DivFrame [mix 1 (1 / df.adws) 0.5]
		local hd : FlatHookDepth dfHook

		local mul : mix 1 (1 / df.adws) 0.75
		local xCrossRight : [mix dfHook.width dfHook.rightSB mul] + (df.middle - dfHook.middle)
		local xBarLeft    : df.middle - [HSwToV : 0.5 * sw]

		include : dispiro
			widths.lhs sw
			flat xCrossRight y4
			curl [Math.min (xBarLeft + hd.x) (xCrossRight - TINY)] y4
			archv
			flat xBarLeft [Math.max y3 (y4 - hd.y)]
			curl xBarLeft (y3 + O)

		include : dispiro
			widths.center sw
			flat df.middle (y2 - O) [heading Downward]
			DiagTail.L df.middle y1 [DiagTail.StdDepth dfHook sw] sw

	define [MtSerif df y sw] : tagged 'serifMT' : HSerif.lt df.middle y (Jut          - [HSwToV : 0.5 * (Stroke - sw)]) sw
	define [MbSerif df y sw] : tagged 'serifMB' : HSerif.mb df.middle y (MidJutCenter - [HSwToV : 0.5 * (Stroke - sw)]) sw

	glyph-block-export yCapitalPhiBowlBot yCapitalPhiBowlTop
	define [yCapitalPhiBowlBot top slab] : mix (0 + [if slab Stroke 0]) (top - [if slab Stroke 0]) 0.125
	define [yCapitalPhiBowlTop top slab] : mix (0 + [if slab Stroke 0]) (top - [if slab Stroke 0]) 0.875

	define [GrekCapitalPhiImpl fFlatTB df] : glyph-proc
		local y2 : yCapitalPhiBowlBot CAP SLAB
		local y3 : yCapitalPhiBowlTop CAP SLAB
		define [object swBar] : include : VarPhiRing fFlatTB df y2 y3 df.archDepthA df.archDepthB
		include : StraightBar df 0 y2 y3 CAP swBar

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle CAP MidJutCenter
			include : tagged 'serifMB' : HSerif.mb df.middle 0   MidJutCenter

	create-glyph 'grek/Phi' 0x3A6 : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.capital
		include : GrekCapitalPhiImpl false df

	create-glyph 'cyrl/Ef' 0x424 : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.capital
		include : GrekCapitalPhiImpl true df

	create-glyph 'cyrl/Ef.BGR' : glyph-proc
		local df : include : DivFrame para.advanceScaleMM 3
		include : df.markSet.capital

		local yExt : Math.max (LongVJut - QuarterStroke) : if SLAB (1.5 * Stroke) 0

		local top : CAP + yExt
		local bot : 0   - yExt

		include : ExtendAboveBaseAnchors top
		include : ExtendBelowBaseAnchors bot

		define [object swBar] : include : VarPhiRing true df 0 CAP df.archDepthA df.archDepthB
		include : StraightBar df bot 0 CAP top swBar

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle top MidJutCenter
			include : tagged 'serifMB' : HSerif.mb df.middle bot MidJutCenter

	create-glyph 'taillessphi' 0x2C77 : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.e
		include : GrekLowerPhiCursiveRing false df 0 XH df.smallArchDepthA df.smallArchDepthB

	create-glyph 'grek/phi.cursive' : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.p
		define [object swBar] : include : GrekLowerPhiCursiveRing false df 0 XH df.smallArchDepthA df.smallArchDepthB
		include : VBar.m df.middle Descender (0.2 * df.mvs) swBar

	create-glyph 'grek/phi.straight' : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.bp
		define [object swBar] : include : VarPhiRing false df 0 XH df.smallArchDepthA df.smallArchDepthB
		include : StraightBar df Descender 0 XH Ascender swBar

	create-glyph 'grek/phi.neohellenic' : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.p
		define [object swBar] : include : VarPhiRing false df 0 XH df.smallArchDepthA df.smallArchDepthB
		include : VBar.m df.middle Descender (0.2 * df.mvs) swBar

	select-variant 'grek/phi' 0x3C6
	alias 'grek/varphi' 0x3D5 'grek/phi.straight'

	create-glyph 'latn/phi' 0x278 : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include [refer-glyph 'grek/varphi'] AS_BASE ALSO_METRICS

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle Ascender  MidJutCenter
			include : tagged 'serifMB' : HSerif.mb df.middle Descender MidJutCenter

	define CyrlLowerEfConfig : SuffixCfg.weave
		object # bowl
			""                    { VarPhiRing      para.advanceScaleM  }
			splitBowl             { CyrlEfSplitRing para.advanceScaleMM }
		object # bar
			serifless             { StraightBar null    null    }
			topSerifed            { StraightBar MtSerif null    }
			serifed               { StraightBar MtSerif MbSerif }
			cursive               { CursiveBar  null    null    }
			diagonalTailedCursive { DiagonalTailCursiveBar null null }

	foreach { suffix { { Bowl adws } { Bar sMT sMB } } } [Object.entries CyrlLowerEfConfig] : do
		create-glyph "cyrl/ef.\(suffix)" : glyph-proc
			local df : include : DivFrame adws 3
			include : df.markSet.bp
			define [object swBar] : include : Bowl true df 0 XH df.smallArchDepthA df.smallArchDepthB
			include : Bar df Descender 0 XH Ascender swBar
			if sMT : include : sMT df Ascender  swBar
			if sMB : include : sMB df Descender swBar

	select-variant 'cyrl/ef' 0x444
	select-variant 'cyrl/ef.BGR' (shapeFrom -- 'cyrl/ef')
