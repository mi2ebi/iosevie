$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Latin-Hwair : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : LeaningAnchor
	glyph-block-import Letter-Shared : CreateSelectorVariants DefineSelectorGlyph
	glyph-block-import Letter-Shared-Shapes : nShoulder uBowl SerifFrame

	define HConfig : object
		straightSerifless                { false false }
		straightTopLeftSerifed           { true  false }
		straightSerifedExceptBottomRight { true  true  }

	define VConfig : object
		roundedSerifless false
		roundedSerifed   true

	foreach { suffix { serifLT serifLB } } [pairs-of HConfig] : do
		local df : DivFrame para.advanceScaleMM 3
		DefineSelectorGlyph "hwair" suffix df 'b'

		foreach { suffixV serifRT } [Object.entries VConfig] : do
			create-glyph "hwair.\(suffix).\(suffixV)" : glyph-proc
				set-width 0
				set-mark-anchor 'cvDecompose' 0 0
				include : LeaningAnchor.Above.VBar.l df.leftSB df.mvs
				local ada : SmallArchDepthA * (2 / df.hPack) * df.adws
				local adb : SmallArchDepthB * (2 / df.hPack) * df.adws
				include : VBar.l df.leftSB 0 Ascender df.mvs
				include : let [yJoin : mix 0 XH 0.5] : union
					nShoulder.shape
						top     -- XH
						bottom  -- (yJoin - TINY)
						left    -- (df.leftSB + [HSwToV df.mvs])
						right   -- (df.middle + [HSwToV : 0.5 * df.mvs])
						stroke  -- df.mvs
						fine    -- df.shoulderFine
						ada     -- ada
						adb     -- adb
					uBowl.toothlessRounded
						top     -- (yJoin + TINY)
						bottom  -- 0
						left    -- (df.middle - [HSwToV : 0.5 * df.mvs])
						right   -- df.rightSB
						stroke  -- df.mvs
						ada     -- ada
						adb     -- adb
						rightY0 -- XH
				local sf : SerifFrame.fromDf df Ascender 0
				if serifLT : include : tagged 'serifLT' sf.lt.outer
				if serifLB : include : tagged 'serifLB' sf.lb.full
				if serifRT : begin
					local sf2 : SerifFrame.fromDf df XH 0
					include : tagged 'serifRT' sf2.rt.full

		select-variant "hwair.\(suffix)" (follow -- 'hv/v')

	CreateSelectorVariants 'hwair' 0x195 [Object.keys HConfig] (follow -- 'hv/h')
