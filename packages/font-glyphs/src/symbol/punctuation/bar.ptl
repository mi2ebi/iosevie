### Punctuation symbols
$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [Joining] from "@iosevka/glyph/relation"
import [DesignParameters] from "../../meta/aesthetics.mjs"

glyph-module

glyph-block Symbol-Punctuation-Bar : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : ExtendAboveBaseAnchors ExtendBelowBaseAnchors

	glyph-block-export BarShape
	define [BarShape x top bot _sw markSet] : glyph-proc
		if markSet
		: then : begin
			include markSet
			include : ExtendAboveBaseAnchors top
			include : ExtendBelowBaseAnchors bot
		: else : begin
			set-base-anchor 'above' x top
			set-base-anchor 'below' x bot
		include : VBar.m x bot top _sw

	create-glyph 'bar.upright'              : BarShape Middle ParenTop ParenBot
	create-glyph 'bar.slanted.naturalSlope' : BarShape Middle ParenTop ParenBot
	create-glyph 'bar.slanted.forceUpright' : glyph-proc
		include : ForceUpright
		include : BarShape Middle ParenTop ParenBot

	create-glyph 'ligBarInsideBracketLeft' : glyph-proc
		Joining.set currentGlyph Joining.Classes.Left
		local gap : Stroke + [Math.max [AdviceStroke 5] (XH / 12)]
		include : BarShape
			[mix SB RightSB DesignParameters.bracketOutside] + [HSwToV HalfStroke]
			ParenTop - gap
			ParenBot + gap

	create-glyph 'ligBarInsideBracketRight' : glyph-proc
		Joining.set currentGlyph Joining.Classes.Right
		local gap : Stroke + [Math.max [AdviceStroke 5] (XH / 12)]
		include : BarShape
			[mix RightSB SB DesignParameters.bracketOutside] - [HSwToV HalfStroke]
			ParenTop - gap
			ParenBot + gap

	define [BrokenBarShape x top bot _sw] : glyph-proc
		local breakDist : Math.max Stroke (CAP / 8)
		local yMid : mix top bot 0.5
		include : VBar.m x (yMid + breakDist / 2) top _sw
		include : VBar.m x bot (yMid - breakDist / 2) _sw

	create-glyph 'brokenBar.upright'              : BrokenBarShape Middle ParenTop ParenBot
	create-glyph 'brokenBar.slanted.naturalSlope' : BrokenBarShape Middle ParenTop ParenBot
	create-glyph 'brokenBar.slanted.forceUpright' : glyph-proc
		include : ForceUpright
		include : BrokenBarShape Middle ParenTop ParenBot

	glyph-block-export DoubleBarShape
	define [DoubleBarShape x top bot _sw markSet] : glyph-proc
		if markSet
		: then : begin
			include markSet
			include : ExtendAboveBaseAnchors top
			include : ExtendBelowBaseAnchors bot
		: else : begin
			set-base-anchor 'above' x top
			set-base-anchor 'below' x bot
		local fine : [AdviceStroke 3.5] * ([fallback _sw Stroke] / Stroke)
		include : VBar.m (x - Width / 6) bot top fine
		include : VBar.m (x + Width / 6) bot top fine

	create-glyph 'doubleBar.upright'              : DoubleBarShape Middle ParenTop ParenBot
	create-glyph 'doubleBar.slanted.naturalSlope' : DoubleBarShape Middle ParenTop ParenBot
	create-glyph 'doubleBar.slanted.forceUpright' : glyph-proc
		include : ForceUpright
		include : DoubleBarShape Middle ParenTop ParenBot

	glyph-block-export TripleBarShape
	define [TripleBarShape x top bot _sw markSet] : glyph-proc
		if markSet
		: then : begin
			include markSet
			include : ExtendAboveBaseAnchors top
			include : ExtendBelowBaseAnchors bot
		: else : begin
			set-base-anchor 'above' x top
			set-base-anchor 'below' x bot
		local fine : [AdviceStroke 4.25] * ([fallback _sw Stroke] / Stroke)
		include : VBar.m (x - Width / 4) bot top fine
		include : VBar.m  x              bot top fine
		include : VBar.m (x + Width / 4) bot top fine

	create-glyph 'tripleBar.upright'              : TripleBarShape Middle ParenTop ParenBot
	create-glyph 'tripleBar.slanted.naturalSlope' : TripleBarShape Middle ParenTop ParenBot
	create-glyph 'tripleBar.slanted.forceUpright' : glyph-proc
		include : ForceUpright
		include : TripleBarShape Middle ParenTop ParenBot

	select-variant 'bar.slanted'
	orthographic-slanted 'bar' '|'

	select-variant 'brokenBar.slanted' (follow -- 'bar.slanted')
	orthographic-slanted 'brokenBar' 0xA6

	select-variant 'doubleBar.slanted' (follow -- 'bar.slanted')
	orthographic-slanted 'doubleBar' 0x2016

	select-variant 'tripleBar.slanted' (follow -- 'bar.slanted')
	orthographic-slanted 'tripleBar' 0x2980

	create-glyph 'dentalClick'  0x1C0 : glyph-proc
		local df : include : DivFrame para.advanceScaleI
		include : BarShape df.middle Ascender Descender df.mvs [df.markSet.bp]
	create-glyph 'lateralClick' 0x1C1 : glyph-proc
		local df : include : DivFrame 1
		include : DoubleBarShape df.middle Ascender Descender df.mvs [df.markSet.bp]
	create-glyph 'palatalClick' 0x1C2 : glyph-proc
		local df : include : DivFrame para.advanceScaleF
		include : BarShape df.middle Ascender Descender df.mvs [df.markSet.bp]
		include : HBar.b df.leftSB df.rightSB ([mix Descender Ascender 0.5] + XH * 0.1) OverlayStroke
		include : HBar.t df.leftSB df.rightSB ([mix Descender Ascender 0.5] - XH * 0.1) OverlayStroke
